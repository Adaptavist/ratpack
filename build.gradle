import org.gradle.plugins.javascript.coffeescript.CoffeeScriptCompile

apply plugin: 'ratpack-groovy'
apply plugin: 'coffeescript-base'

configurations.all {
	resolutionStrategy {
		force 'org.sonatype.sisu.inject:cglib:3.0' // required otherwise pegdown pulls in an older version that conflicts with netty's
	}
}

buildscript {
	repositories {
		maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
		mavenCentral()
	}
	dependencies {
		classpath 'org.ratpack-framework.netty:ratpack-gradle:0.7.0-SNAPSHOT'
	}
}

repositories {
	maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
	mavenCentral()
	maven { url "http://repo.springsource.org/repo" } // for springloaded
	maven { url "http://repo.gradle.org/gradle/javascript-public/" } // for coffeescript plugin
}

configurations {
	jruby
}

dependencies {
	jruby 'org.jruby:jruby-complete:1.7.3'

	compile 'org.pegdown:pegdown:1.2.1'

	testCompile 'org.spockframework:spock-core:0.7-groovy-2.0',
				'org.jodd:jodd-lagarto:3.4.3'
}

task compileCoffee(type: CoffeeScriptCompile) {
	source fileTree('src/main/static/scripts')
	destinationDir file('src/ratpack/public/scripts')
}

ext {
	gemsDir = file('.jruby/gems')
	cssDir = file('src/ratpack/public/styles')
	sassDir = file('src/main/static/styles')
	imagesDir = file('src/ratpack/public/images')
	javascriptsDir = file('src/ratpack/public/scripts')
}

task installGems(type: JRubyExec) {
	outputs.dir gemsDir

	command = "gem install -i $gemsDir --no-rdoc --no-ri compass"
	classpath = configurations.jruby
	gemPath = gemsDir

	doFirst {
		gemsDir.mkdirs()
	}
}

task compileSass(type: JRubyExec) {
	outputs.dir cssDir
	inputs.files installGems
	inputs.dir sassDir
	inputs.dir imagesDir
	inputs.dir javascriptsDir

	command = "compass compile --sass-dir $sassDir --css-dir $cssDir --images-dir $imagesDir --javascripts-dir $javascriptsDir --relative-assets"
	classpath = configurations.jruby
	gemPath = installGems.outputs.files.singleFile

	doFirst {
		cssDir.mkdirs()
	}
}

task watchSass {
	doFirst {
		cssDir.mkdirs()

		Thread.start {
			project.javaexec {
				classpath = configurations.jruby
				main = 'org.jruby.Main'
				jvmArgs '-client -XX:+TieredCompilation -XX:TieredStopAtLevel=1'.tokenize()
				environment 'GEM_PATH', gemsDir
				args "-X-C -S compass watch --sass-dir $sassDir --css-dir $cssDir --images-dir $imagesDir --javascripts-dir $javascriptsDir --relative-assets".tokenize()
			}
		}
	}
}

run.dependsOn watchSass

clean.dependsOn cleanCompileCoffee, cleanCompileSass
processResources.inputs.files compileCoffee
processResources.inputs.files compileSass

task wrapper(type: Wrapper) {
	gradleVersion = '1.5'
}

class JRubyExec extends JavaExec {
	JRubyExec() {
		main = 'org.jruby.Main'
		jvmArgs '-client -XX:+TieredCompilation -XX:TieredStopAtLevel=1'.tokenize()
	}

	void setGemPath(gemPath) {
		environment 'GEM_PATH', gemPath
	}

	void setCommand(String command) {
		args "-X-C -S $command".tokenize()
	}
}
