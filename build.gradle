import org.apache.tools.ant.taskdefs.condition.Os
import org.gradle.api.tasks.Exec

apply plugin: 'ratpack-groovy'

configurations.all {
	resolutionStrategy {
		failOnVersionConflict()
		force 'org.sonatype.sisu.inject:cglib:3.0'
	}
}

buildscript {
	repositories {
		maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
		mavenCentral()
	}
	dependencies {
		classpath 'org.ratpack-framework.netty:ratpack-gradle:0.7.0-SNAPSHOT'
	}
}

repositories {
	maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
	mavenCentral()
	maven { url "http://repo.springsource.org/repo" } // for springloaded
}

dependencies {
	compile('org.pegdown:pegdown:1.2.1')
}

task coffee(type: GruntTask) {
	gruntArgs = "coffee"

	inputs.dir new File('src/main/static/scripts')
	outputs.dir new File('src/ratpack/public/scripts')
}

task compass(type: GruntTask) {
	gruntArgs = "compass"

	inputs.dir new File('src/main/static/styles')
	outputs.dir new File('src/ratpack/public/styles')
}

clean.dependsOn cleanCoffee, cleanCompass
processResources.dependsOn coffee, compass

task wrapper(type: Wrapper) {
	gradleVersion = '1.5'
}

class GruntTask extends Exec {
	private String gruntExecutable = Os.isFamily(Os.FAMILY_WINDOWS) ? "grunt.cmd" : "grunt"
	private String switches = "--no-color"

	String gruntArgs = ""

	public GruntTask() {
		super()
		this.setExecutable(gruntExecutable)
	}

	public void setGruntArgs(String gruntArgs) {
		this.args = "$switches $gruntArgs".trim().split(" ") as List
	}
}