configurations.all { configuration ->
  resolutionStrategy {
    failOnVersionConflict()

    eachDependency {
      // Spock brings in both 1.1 and 1.3 of this guy, so force 1.1 to 1.3.
      // Excludes aren't ideal because it's spock bringing in both of these
      if (requested.name == "hamcrest-core" && requested.version == "1.1") {
        useVersion "1.3"
      }
    }
  }

  incoming.afterResolve {
    it.resolutionResult.allDependencies {
      if (it instanceof ResolvedDependencyResult) {
        if (it.selected.moduleVersion.group == "org.codehaus.groovy" && it.selected.moduleVersion.name == "groovy") {
          throw new InvalidUserDataException("groovy in dependency graph of $configuration.name of project $project.name")
        }
      }
    }
  }
}
