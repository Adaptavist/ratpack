apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'jetty'

group = 'com.bleedingwolf.ratpack'
version = '0.2-SNAPSHOT'

repositories {
  mavenCentral()
}

configurations { 
  provided
  jetty
}

dependencies {
  groovy 'org.codehaus.groovy:groovy:1.8.3'
  provided 'javax.servlet:servlet-api:2.5'
  compile 'org.json:json:20090211'

  compile(group: 'org.mortbay.jetty', name: 'jetty', version: '6.1.26') {
    exclude module: 'servlet-api'
  }

  compile(group: 'org.mortbay.jetty', name: 'jetty-util', version: '6.1.26') {
    exclude module: 'servlet-api'
  }

  compile 'org.slf4j:slf4j-api:1.6.3'
  testCompile 'junit:junit:4.10'
  runtime 'org.slf4j:slf4j-simple:1.6.3'
}

sourceSets {
  main {
    groovy {
      compileClasspath += configurations.provided
    }
  }
  app {
    groovy {
      srcDir 'src/app/groovy'
    }
  }
}

idea {
  module {
    scopes.PROVIDED.plus += configurations.provided
  }
}

war {
  webInf {
    from sourceSets.app.groovy
  }
}

jettyRunWar {
  contextPath = '/'
  httpPort = 5000
}


task wrapper(type: Wrapper) {
  gradleVersion = '1.0-milestone-6'
}

task run(type: JavaExec) {
  main = 'com.bleedingwolf.ratpack.RatpackRunner'
  args = [ 'src/app/groovy/app.groovy' ]
  classpath(runtimeClasspath + configurations.provided)
}

task copyToLib(type: Copy) {
  into "$buildDir/libs"
  from configurations.runtime
}

task deployRatpack(dependsOn: [build,copyToLib], type: Copy) {
  into "${System.properties['user.home']}/.groovy/lib"
  from "$buildDir/libs"
  include "*.jar"
}

task buildDistro(dependsOn: assemble) << {
  copy {
    from "$projectDir/src/main/binaries"
    into "$buildDir/ratpack/bin"
  }
  ant.chmod(file: "$buildDir/ratpack/bin/ratpack", perm: 'ugo+x')
  copy {
    from configurations.runtime
    into "$buildDir/ratpack/lib"
  }
  copy {
    from "$buildDir/libs"
    into "$buildDir/ratpack/lib"
    include "Ratpack-*.jar"
  }
}
