buildscript {
  repositories {
    maven { url "http://dl.bintray.com/robfletcher/gradle-plugins" }
    mavenCentral()
  }
  dependencies {
    classpath 'org.gradle.plugins:gradle-compass:1.0'
  }
}

import org.gradle.plugins.javascript.coffeescript.CoffeeScriptCompile
import org.gradle.plugins.compass.*

apply plugin: 'coffeescript-base'
apply plugin: 'compass'
apply plugin: "ratpack-groovy"

// Remove the dependencies that the plugin added (we'll use project dependencies)
configurations.compile.dependencies.clear()

repositories {
  mavenCentral()
  maven { url "http://repo.springsource.org/repo" } // for springloaded
  maven { url "http://repo.gradle.org/gradle/javascript-public/" } // for coffeescript plugin
}

configurations {
  latestManual {}
}

dependencies {
  springloaded "org.springsource.springloaded:springloaded-core:1.1.1"
  compile project(":ratpack-groovy")
  latestManual project(":ratpack-manual")
}

task gatherManuals(type: Sync) {
  inputs.files configurations.latestManual
  into "src/ratpack/public/manual"
  ["snapshot", version].each {
    into it, {
      from { zipTree(configurations.latestManual.singleFile) }
    }
  }
}

task prepareContent {
  dependsOn gatherManuals
}

run {
  dependsOn configureRun, prepareContent
  jvmArgs "-Dratpack.reloadable=true"
}

installApp {
  dependsOn prepareContent
  from run.workingDir
}

task compileCoffee(type: CoffeeScriptCompile) {
	source fileTree('src/main/static/scripts')
	destinationDir file('src/ratpack/public/scripts')
}

processResources.inputs.files compileCoffee

compass {
	cssDir = file('src/ratpack/public/styles')
	sassDir = file('src/main/static/styles')
	imagesDir = file('src/ratpack/public/images')
	javascriptsDir = file('src/ratpack/public/scripts')
}

clean.dependsOn cleanCompileSass
run.dependsOn watchSass
installApp.dependsOn compileSass

task createScriptsDir {
  doLast {
    mkdir "src/ratpack/public/scripts"
  }
}
configure([watchSass, compileSass]) {
  dependsOn createScriptsDir
}

clean.dependsOn cleanCompileCoffee
clean.dependsOn cleanGatherManuals
