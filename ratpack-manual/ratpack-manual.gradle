apply plugin: 'compass'

buildscript {
  repositories {
    mavenCentral()
    maven { url 'http://dl.bintray.com/robfletcher/gradle-plugins' }
  }
  dependencies {
    classpath 'org.gradle.plugins:gradle-compass:1.0'
    classpath "org.gebish:markdown2book:1.0"
    classpath "org.pegdown:pegdown:1.2.1"
  }
}

apply from: "$rootDir/gradle/checkstyle.gradle"

archivesBaseName = "ratpack-manual"

modifyPom { pom ->
  project {
    name "Ratpack Manual"
  }
}

task tokeniseManual(type: Sync) {
  from "src"
  into "$buildDir/manual-tokenised"

  ext.substitutionProperties = [
      title: "Ratpack Manual",
      tagline: "A Micro Web Framework for Java & Groovy",
      "ratpack-version": project.version,
      "ratpack-group": project.group,
      "groovy-version": groovyVersion,
      "manual-url": "https://oss.sonatype.org/content/repositories/${isSnapshot ? 'snapshots' : 'releases'}/org/ratpack-framework/ratpack-manual/$version/ratpack-manual-${version}.zip".toString(),
      head: """
        <meta charset="utf-8">
        <meta name="viewport" content="width=device-width">
        <link href="http://fonts.googleapis.com/css?family=Cabin+Condensed:700|Libre+Baskerville:400,700,400italic|Engagement" rel="stylesheet" type="text/css">
        <link href="css/stylesheet.css" rel="stylesheet" type="text/css">
        <script src="js/modernizr.js"></script>
        <script src="js/prism.js"></script>
        <script src="js/jquery-1.9.1.min.js"></script>
      """,
      footer: """
        <p>Ratpack Manual - $version - ${new Date().format("MMMM, yyyy")}</p>
        <p><a href="http://www.ratpack-framework.org">www.ratpack-framework.org</a></p>
        <p>Licensed under the <a href="http://www.apache.org/licenses/LICENSE-2.0">Apache License, Version 2.0</a></p>
      """.toString()
  ]

  inputs.properties(substitutionProperties)

  def tokenisableExtensions = ["md", "html"]
  inputs.property("tokenisableExtensions", tokenisableExtensions)

  eachFile { file ->
    if (tokenisableExtensions.any { file.name.endsWith(it) }) {
      file.filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: substitutionProperties)
    }
  }
}

configurations {
  groovydoc
}

apiModules.each { evaluationDependsOn(it.path) }

task api(type: Javadoc) {
  classpath = files()
  destinationDir = file("$buildDir/api")
  options.use true

  apiModules.each {
    source it.sourceSets.main.java
    classpath += it.sourceSets.main.compileClasspath + it.sourceSets.main.output

    options.links("http://docs.oracle.com/javase/7/docs/api")

    if (it.hasProperty("apiLinks")) {
      options.links(*it.apiLinks)
    }
  }

  exclude "**/internal/**"

}

task checkstyleApi(type: Checkstyle) {
  source api.source
  configFile = rootProject.file("gradle/checkstyle/checkstyle-api.xml")
  classpath = files()
  reports.xml.destination = reporting.file("checkstyle-api.xml")
  ignoreFailures true
}

task compileManual {
  dependsOn tokeniseManual

  ext.source = tokeniseManual.destinationDir
  inputs.dir(source)

  ext.destination = file("$buildDir/manual-compiled")
  outputs.dir(destination)

  ext.encoding = "UTF-8"
  inputs.property("encoding", encoding)

  doLast {
    new markdown2book.Generator(source, destination, encoding).generate()
  }
}

task packageManual(type: Sync) {
  into "$buildDir/manual"
  from compileManual

  into ("api") {
    from api
  }
}

task openManual() {
  doLast {
    new java.awt.Desktop().browse(file("$packageManual.destinationDir/index.html").toURI())
  }
}

task openApi(dependsOn: api) {
  doLast {
    new java.awt.Desktop().browse(file("$api.destinationDir/index.html").toURI())
  }
}

task manualZip(type: Zip) {
  from packageManual
}

artifacts {
  archives manualZip
}

task check(dependsOn: checkstyleApi)
task build(dependsOn: [assemble, check])


compass {
  cssDir = file('src/css')
  sassDir = file('src/sass')
  imagesDir = file('src/images')
  javascriptsDir = file('src/js')
}

compileManual.inputs.files compileSass