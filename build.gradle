import org.gradle.plugins.javascript.coffeescript.CoffeeScriptCompile
import org.gradle.plugins.compass.*

apply plugin: 'ratpack-groovy'
apply plugin: 'coffeescript-base'
apply plugin: CompassPlugin

configurations.all {
	resolutionStrategy {
		force 'org.sonatype.sisu.inject:cglib:3.0' // required otherwise pegdown pulls in an older version that conflicts with netty's
	}
}

buildscript {
	repositories {
		maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
		mavenCentral()
	}
	dependencies {
		classpath 'org.ratpack-framework.netty:ratpack-gradle:0.7.0-SNAPSHOT'
	}
}

repositories {
	maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
	mavenCentral()
	maven { url "http://repo.springsource.org/repo" } // for springloaded
	maven { url "http://repo.gradle.org/gradle/javascript-public/" } // for coffeescript plugin
}

dependencies {
	compile 'org.pegdown:pegdown:1.2.1'

	testCompile 'org.spockframework:spock-core:0.7-groovy-2.0',
			'org.jodd:jodd-lagarto:3.4.3'
}

task compileCoffee(type: CoffeeScriptCompile) {
	source fileTree('src/main/static/scripts')
	destinationDir file('src/ratpack/public/scripts')
}
processResources.inputs.files compileCoffee
clean.dependsOn cleanCompileCoffee

compass {
	cssDir = file('src/ratpack/public/styles')
	sassDir = file('src/main/static/styles')
	imagesDir = file('src/ratpack/public/images')
	javascriptsDir = file('src/ratpack/public/scripts')
}

processResources.inputs.files compileSass
clean.dependsOn cleanCompileSass
run.dependsOn watchSass

task wrapper(type: Wrapper) {
	gradleVersion = '1.5'
}
